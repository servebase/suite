require! <[crypto bcrypt argon2 lderror re2js curegex @loadingio/debounce.js]>

re-email = curegex.tw.get('email', re2js.RE2JS)
is-email = -> return re-email.exec(it)

user-store = (opt = {}) ->
  @config = opt.config or {}
  @policy =
    login: (@config.policy or {}).login or {}
    password: pw = (@config.policy or {}).password or {}
  pw.renew = if !pw.renew or isNaN(pw.renew) => 0 else (+pw.renew >?= 1)
  if typeof(pw.track) == \object =>
    pw.track.day = if !pw.track.day or isNaN(pw.track.day) => 0 else (+pw.track.day >?= 1)
    pw.track.count = if !pw.track.count or isNaN(pw.track.count) => 0 else (+pw.track.count >?= 1)
  else
    pw.track = {day: if !pw.track or isNaN(pw.track) => 0 else (+pw.track >?= 1)}
  # validate and cache pepper at init time so misconfiguration fails fast on startup
  @pepper = (@config?auth?pepper or {}){keys or {}, current or null}
  if @pepper.current and !@pepper.keys[@pepper.current]
    throw new Error "pepper key '#{ @pepper.current }' not found in config.auth.pepper.keys"
  @db = opt.db
  @

user-store.prototype = Object.create(Object.prototype) <<< do
  # store whole object ( no serialization )
  serialize: (u = {}) -> Promise.resolve u
  deserialize: (v = {}) -> Promise.resolve v

  hashing: (password) ->
    pw = "#password".substring(0, 1024)
    opts = type: argon2.argon2id, memoryCost: 65536, timeCost: 3, parallelism: 4, hashLength: 32
    if @pepper.current and (p = @pepper.keys[@pepper.current]) => opts.secret = Buffer.from p
    (hash) <~ argon2.hash pw, opts .then _
    if @pepper.current => "#{ @pepper.current }:#hash" else hash

  compare: (password='', hash) ->
    m = /^([^:]+):(\$argon2id\$.+)$/.exec hash
    if m
      [_, name, argon-hash] = m
      if !@pepper.keys[name] => return Promise.reject new Error "pepper key '#name' not found in config"
      opts = {secret: Buffer.from @pepper.keys[name]}
      current = @pepper.current
      (ok) <- argon2.verify argon-hash, password, opts .then _
      if ok => {legacy: name != current}
      else Promise.reject new lderror 1012
    else if hash and hash.indexOf(\$argon2id$) == 0
      # no pepper argon2id → legacy
      (ok) <- argon2.verify hash, password, {} .then _
      if ok => {legacy: true} else Promise.reject new lderror 1012
    else
      # legacy scheme: MD5 + bcrypt
      md5 = crypto.createHash(\md5).update(password).digest(\hex)
      (res, rej) <- new Promise _
      (e, ret) <- bcrypt.compare md5, hash, _
      if ret => res {legacy: true} else rej new lderror 1012

  get: ({username, password, method, detail, create, invite-token}) ->
    username = username.toLowerCase!
    if !(is-email username) => return Promise.reject new lderror(1015)
    @db.query "select * from users where username = $1", [username]
      .then (ret = {}) ~>
        if !(user = ret.[]rows.0) and !create => return lderror.reject 1034
        if !user => return @create {username, password, method, detail, invite-token}
        if !(method == \local or user.method == \local) =>
          delete user.password
          return user
        @compare password, user.password .then ({legacy}) ~>
          if legacy =>
            (new-hash) <~ @hashing password .then _
            # update user with latest hash algorithm. silently fail in case of service interruption
            @db.query "update users set password = $2 where key = $1", [user.key, new-hash] .catch -> void
          user
      .then (user) ~>
        if user.{}config.{}consent.cookie => return user
        user.config.consent.cookie = new Date!getTime!
        @db.query "update users set config = $2 where key = $1", [user.key, user.config] .then -> user
      .then (user) ->
        delete user.password
        return user

  create: ({username, password, method, detail, config, invite-token, force}) ->
    policy = @policy.login
    if !force and policy.accept-signup? and (!policy.accept-signup or policy.accept-signup == \no) =>
      return lderror.reject 1040

    username = username.toLowerCase!
    if !config => config = {}
    if !is-email(username) => return lderror.reject 1015
    Promise.resolve!
      .then ~> if method == \local => @hashing(password) else password
      .then (password) ~>
        displayname = if detail => detail.displayname or detail.username
        if !displayname => displayname = username.replace(/@[^@]+$/, "")
        verified = if method == \local or !(policy and policy.oauth-default-verified) => null
        else {date: Date.now!}
        config.{}consent.cookie = new Date!getTime!
        user = { username, password, method, displayname, detail, config, createdtime: new Date! }
        if verified => user <<< {verified}
        @db.query "select key from users where username = $1", [username]
          .then (r={}) ~>
            if r.[]rows.length => return lderror.reject 1014
            p = if force or policy.accept-signup != \invite => Promise.resolve null
            else @db.query """select * from invitetoken where token = $1 and deleted is not true""", [invite-token]
          .then (r) ~>
            if r and !(token = r.[]rows.0) => return lderror.reject 1043 # token required
            if !token => return
            if token.ttl and (isNaN(ttl = new Date(token.ttl).getTime!) or ttl <= Date.now!) =>
              return lderror.reject 1043 # token expired. consider it as no token so token required
            detail = token.detail or {}
            if !detail.count => return
            if (detail.used or 0) >= detail.count => return lderror.reject 1004
            token.detail.used = token.detail.used + 1
            config.{}invite-token[invite-token] = {createdtime: Date.now!}
            @db.query "update invitetoken set detail = $2 where key = $1", [token.key, token.detail]
          .then ~>
            @db.query """
            insert into users (username,password,method,displayname,createdtime,detail,config,verified)
            values ($1,$2,$3,$4,$5,$6,$7,$8)
            returning key
            """, [
              username, password, method, displayname,
              new Date!toUTCString!, detail, config, verified
            ]
          .then (r={}) ~>
            if !(r = r.[]rows.0) => return Promise.reject 500
            user <<< r{key}
            @password-track {user, hash: password}
          .then -> user

  password-track: ({user, password, hash}) ->
    policy = @policy.password
    if !(policy.track.day or policy.track.count or policy.renew)
    or !(hash or password) => return Promise.resolve!
    # debounce password track to control tracking frequency
    debounce 1000
      .then -> if hash => that else @hashing(password)
      .then (hash) ~> @db.query "insert into password (owner, hash) values ($1, $2)", [user.key, hash]
      .then ~>
        # it's possible that we are here even if track is not configured.
        # in this case, we track for a minimal amount. (count = 1, day = 1)
        count = policy.track.count >? 1
        (r={}) <~ @db.query """
        select key from password
        where owner = $1
        order by key desc limit $2
        """, [user.key, count] .then _
        if !(p = r.[]rows[* - 1]) => return
        @db.query "delete from password where owner = $1 and key < $2", [user.key, p.key]
      .then ~>
        day = policy.track.day >? 1
        @db.query """
        delete from password
        where owner = $1 and createdtime < now() - make_interval(0,0,$2)
        """, [user.key, day]

  password-due: ({user}) ->
    policy = @policy.password
    # always not due ( within 180 days ) if renew is not enabled.
    if !policy.renew => return Promise.resolve(-180 * 86400 * 1000)
    @db.query """
    select * from password
    where owner = $1
    order by createdtime desc limit 1
    """, [user.key]
      .then (r={}) ~>
        freq = policy.renew * (86400 * 1000)
        now = Date.now!
        checktime = if (entry = r.[]rows.0) =>
          # use max so we can use a future snooze to delay renewal reuqest
          Math.max(
            new Date(entry.snooze or 0).getTime!,
            new Date(entry.createdtime).getTime! + freq
          )
        else new Date(user.createdtime).getTime! + freq
        return now - checktime
  ensure-password-unused: ({user, password}) ->
    track = @policy.password.track
    if !(track.day or track.count) =>
      qs = "select * from password where owner = $1 order by key desc limit 1"
    else
      qs = "select * from password where owner = $1"
      if track.day => qs += " and createdtime >= now() - make_interval(0,0,0,$2)"
      qs += " order by key desc"
      if track.count => qs += " limit $#{if track.day => 3 else 2}"
    params = (
      [user.key] ++
      (if track.day => [track.day >? 1] else []) ++
      (if track.count => [track.count >? 1] else [])
    )
    @db.query qs, params
      .then (r={}) ~>
        ps = r.[]rows.map (p) ~> @compare(password, p.hash).then(->1).catch(->0)
        Promise.all ps
      .then (r=[]) ->
        if r.filter(->it).length => return lderror.reject 1036

module.exports = user-store
